program -> declaration* EOF

declaration -> letDeclaration | funDeclaration | statement

statement -> block
           | expressionStatement
           | forStatement
           | ifStatement
           | returnStatement
           | whileStatemen

block -> "do" declaration* "end"

expressionStatement -> expression ";"

forStatement -> "for" ( declaration | expressionStatement | ";" ) expression? ";" expression? block

ifStatement -> "if" expression block ( "else" ( ifStatement | block ) )?

returnStatement -> "return" expression? ";"

whileStatement -> "while" expression block

breakStatement -> "break" ( expression )? ";"

letDeclaration -> "let" IDENTIFIER ( "=" expression )? ";"

funDeclaration -> "fun" function

function -> IDENTIFIER ( "(" parameters? ")" )? declaration* "end"

parameters -> IDENTIFIER ( "," IDENTIFIER )*

expression -> lambda

lambda -> "\" parameters? "->" expression | sequence

sequence -> assignment ( "," assignment )*

assignment -> IDENTIFIER
              ( "=" | "+=" | "-=" | "*=" | "/=" | "%="
              | "<<=" | ">>=" | "&=" | "^=" | "|=" )
              assignment | conditional

conditional -> logicalOr ( "?" expression ":" conditional )*

logicalOr -> logicalAnd ( "or" logicalAnd )*

logicalAnd -> bitwiseOr ( "and" bitwiseOr )*

bitwiseOr -> bitwiseXor ( "|" bitwiseXor )*

bitwiseXor -> bitwiseAnd ( "^" bitwiseAnd )*

bitwiseAnd -> equality ( "&" equality )*

equality -> comparison ( ( "!=" | "==" ) comparison )*

comparison -> shift ( ( ">" | ">=" | "<" | "<=" ) shift )*

shift -> addition ( ( ">>" | "<<" ) addition )*

addition -> multiplication ( ( "-" | "+" ) multiplication )*

multiplication -> unary ( ( "/" | "*" | "%" ) unary )*

unary -> ( "!" | "-" | "~" | "++" | "--" ) unary | call

postfix -> primary ( "(" arguments? ")" | "++" | "--" )*

arguments -> expression ( "," expression )*

primary -> NUMBER | STRING | "false" | "true" | "nil"
         | "(" expression ")"
         | IDENTIFIER
